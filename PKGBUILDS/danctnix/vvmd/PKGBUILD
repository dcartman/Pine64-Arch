# Maintainer: Aren Moynihan <aren@peacevolution.org>
pkgname=vvmd
pkgver=1.0
pkgrel=3
pkgdesc="Visual Voicemail Daemon"
url="https://gitlab.com/kop316/vvmd"
arch=('x86_64' 'armv7h' 'aarch64')
license=("GPL2")
install=vvmd.install
depends=("c-ares" "mobile-broadband-provider-info" "libmm-glib" "libphonenumber")
makedepends=("meson")
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/kop316/vvmd/-/archive/$pkgver/vvmd-$pkgver.tar.gz"
        "vvmd.service")
sha256sums=('cde2bfb2e289c618d78994205204806575603b3894d81f5c18b2b6badadf0dd3'
            'b4bfd3b3758427975eee8ca5b4a39fb43ce7ea36b56d7c82a23fdacdb4161c50')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 "vvmd.service" "$pkgdir/usr/lib/systemd/user/vvmd.service"
}
