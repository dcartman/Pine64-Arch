# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-mobile
pkgver=6.2.5
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=1
pkgdesc="Plasma shell for mobile devices"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
depends=(
  bluez-qt
  libplasma
  kirigami-addons
  milou
  plasma-nano
  plasma-nm
  plasma-pa
  plasma-workspace=$pkgver
)
makedepends=(
  cmake
  extra-cmake-modules
)
optdepends=('hfd-service: Haptic feedback')
replaces=('plasma-phone-components')
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig})
install=$pkgname.install
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D')
sha512sums=('d08b996f31962ff4cdc07746a80238fdbe9dc78bebc282e9f42806966451ff2723ef307da792c2d8679a9c6604d52e4d2622b05765c29ba2276356f0e1b1a2be'
            'SKIP')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build -- -j1
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
